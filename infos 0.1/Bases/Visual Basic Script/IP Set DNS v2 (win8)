'On Error Resume Next

Dim Question
Question = MsgBox("Activer le DNS?",vbYesNoCancel+VbQuestion,"")
if Question = VbCancel then WScript.Quit  

if NortonConnectSafe(Question = VbYes) then
  MsgBox "Le service NortonConnectSafe est activé avec succès !"
else
  MsgBox "Le service NortonConnectSafe est désactivé avec succès !"
end if

function NortonConnectSafe(Active)
  If Active = True Then 
    NortonConnectSafe = setDNS(Array("184.169.223.35", "199.85.126.30"))
  else
    NortonConnectSafe = setDNS(null)
  end if
end function


function setDNS(listIP)
    Dim res,objNetCard,arrDNSServers,objWMIService,colNetCards
    res = False
    Call GetAdminPrivilege()
    Set objWMIService = GetObject("winmgmts:{impersonationLevel=impersonate}!\\.\root\cimv2")
    Set colNetCards = objWMIService.ExecQuery("Select * From Win32_NetworkAdapterConfiguration Where IPEnabled = True")
    For Each objNetCard in colNetCards
      objNetCard.SetDNSServerSearchOrder(listIP)
      If Err = 0 Then res = True
    Next
    setDNS = res
End function


sub GetAdminPrivilege()
  Dim WMI, OS, Value, Shell
  do while WScript.Arguments.Count = 0 and WScript.Version >= 5.7
    Set WMI = GetObject("winmgmts:{impersonationLevel=impersonate}!\\.\root\cimv2")
    Set OS = WMI.ExecQuery("SELECT *FROM Win32_OperatingSystem")
    For Each Value in OS
      if left(Value.Version, 3) < 6.0 then exit do
    Next
    Set Shell = CreateObject("Shell.Application")
    Shell.ShellExecute "wscript.exe", """" & WScript.ScriptFullName & """ uac", "", "runas"
    WScript.Quit
  loop
end sub