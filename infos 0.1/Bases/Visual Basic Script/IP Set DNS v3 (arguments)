'Use: this.vbs ip1 ip2
'Use: this.vbs [nodns|nortondns|level3|opendns]

'Example: 
'   this.vbs nodns
'   this.vbs nortondns
'   this.vbs level3
'   this.vbs opendns
'   this.vbs 127.0.0.1 192.168.0.1

if WScript.Arguments.Count = 0 then
  Wscript.Echo "No argument set"
  WScript.Quit  
end if 

if WScript.Arguments.Count = 1 then
  if WScript.Arguments(0) = "nodns" then
    setDNS(null)
    WScript.Quit  
  end if
  if WScript.Arguments(0) = "nortondns" then
    setDNS(Array("184.169.223.35", "199.85.126.30"))
    WScript.Quit  
  end if
  if WScript.Arguments(0) = "level3" then
    setDNS(Array("209.244.0.3", "209.244.0.4"))
    WScript.Quit  
  end if
  if WScript.Arguments(0) = "opendns" then
    setDNS(Array("208.67.222.123", "208.67.220.123"))
    WScript.Quit  
  end if
end if 

Dim listOfDNS(), i
i = 0
redim listOfDNS(WScript.Arguments.Count-1)
For Each arg in WScript.Arguments
  listOfDNS(i) = arg
  i = i + 1
Next
setDNS(listOfDNS)


function setDNS(listIP)
    Dim res,objNetCard,arrDNSServers,objWMIService,colNetCards
    res = False
    Call GetAdminPrivilege()
    Set objWMIService = GetObject("winmgmts:{impersonationLevel=impersonate}!\\.\root\cimv2")
    Set colNetCards = objWMIService.ExecQuery("Select * From Win32_NetworkAdapterConfiguration Where IPEnabled = True")
    For Each objNetCard in colNetCards
      objNetCard.SetDNSServerSearchOrder(listIP)
      If Err = 0 Then res = True
    Next
    setDNS = res
End function


sub GetAdminPrivilege()
  Dim WMI, OS, Value, Shell
  do while WScript.Arguments.Count = 0 and WScript.Version >= 5.7
    Set WMI = GetObject("winmgmts:{impersonationLevel=impersonate}!\\.\root\cimv2")
    Set OS = WMI.ExecQuery("SELECT *FROM Win32_OperatingSystem")
    For Each Value in OS
      if left(Value.Version, 3) < 6.0 then exit do
    Next
    Set Shell = CreateObject("Shell.Application")
    Shell.ShellExecute "wscript.exe", """" & WScript.ScriptFullName & """ uac", "", "runas"
    WScript.Quit
  loop
end sub